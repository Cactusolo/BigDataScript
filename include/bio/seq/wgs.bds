#!/usr/bin/env bds

#-------------------------------------------------------------------------------
#
# Genome sequencing functions
# References:
#
#
# Dependencies:
#	- SAMtools : http://samtools.sourceforge.net/
#	- BWA      : http://bio-bwa.sourceforge.net/
#	- tabix    : http://samtools.sourceforge.net/
#	- GATK     : http://www.broadinstitute.org/gatk/
#
#															Pablo Cingolani 2014
#-------------------------------------------------------------------------------

#-------------------------------------------------------------------------------
# Command line parameters
#-------------------------------------------------------------------------------

# Other options
splitNumReads	:= 100 * 1000		# Split fastq files to 'splitNumReads'
mapSortMemory	:= 1 * G			# Memory used while sorting BAM file (samtools sort)

#-------------------------------------------------------------------------------
# Common variables
#-------------------------------------------------------------------------------

# Extensions used in FASTQ files
fastqExtentions	:= [".gz", ".fq", ".fastq"]

#-------------------------------------------------------------------------------
# Base name, remove all common extensions
#-------------------------------------------------------------------------------
string removeExtFastq(string fq) {
	rmExt := fq
	for( string ext : fastqExtentions ) {
		rmExt = rmExt.removeExt(ext)
	}
	return( rmExt )
}

#-------------------------------------------------------------------------------
# Map reads to genome (split into smaller files)
#-------------------------------------------------------------------------------
string map(string referenceFasta, string fq1, string fq2) {
	# Split files into 
	print("Splitting fastq files\n")
	fq1names := splitFatsq(fq1, splitNumReads)
	fq2names := splitFatsq(fq2, splitNumReads)
	wait

	# Get split files
	fq1split := splitFatsqNames( fq1 )
	fq2split := splitFatsqNames( fq2 )
	print("Fastq splits:\n\t$fq1split\n\t$fq2split\n")

	# Map each pair of files
	string[] bams
	for( int i=0 ; i < fq1split.size() ; i++ ) {
		fqs1 := fq1split[i]
		fqs2 := ""
		if( fq2split )  fqs2 = fq2split[i]

		print("Mapping:\t$fqs1\t$fqs2\n")
		bam := mapSingle(referenceFasta, fqs1, fqs2)
		bams.add( bam )
	}
	wait

	# Merge all sorted BAM files
	bam := removeExtFastq(fq1) + ".bam"
	bamsStr := bams.join(" ")
	task( bam <- bams ) {
		sys samtools merge -f -@ $cpus $bam $bamsStr
	}

	return( bam )
}

#-------------------------------------------------------------------------------
# Index genome (prepare for mapping reads)
#-------------------------------------------------------------------------------
void mapIndex(string fasta) {
	indexFile := fasta + ".bwt"
	task( indexFile <- fasta ) {
		sys bwa index fasta
	}
	wait
}

#-------------------------------------------------------------------------------
# Map reads to genome (single file, no splitting)
# Note: It does not wait
#-------------------------------------------------------------------------------
string mapSingle(string referenceFasta, string fq1, string fq2) {
	bam := removeExtFastq(fq1) + ".bam"
	task( bam <- [fq1, fq2] ) {
		sys bwa mem -t $cpus $referenceFasta $fq1 $fq2 \
			| samtools view -S -u - \
			| samtools sort -@ $cpus -m $mapSortMemory -f - $bam
	}

	return( bam )
}

#-------------------------------------------------------------------------------
# Run task  to split a fastq file into 'splitNumReads' reads (does not wait)
# Return file names
#-------------------------------------------------------------------------------
string splitFatsq(string fq,int splitNumReads) {
	# Empty name? Nothing to do
	if( ! fq )	return("")

	# Compressed files use gunzip
	cat := "cat"
	if( fq.endsWith('.gz') )	cat = "gunzip -c"	

	names := removeExtFastq( fq ) + "."
	numLines := 4 * splitNumReads
	firstName := names + "aa"
	task( firstName <- fq ) {
		sys $cat $fq | split -l $numLines - $names
	}
	return( names )
}

#-------------------------------------------------------------------------------
# Get names form split fastq files
#-------------------------------------------------------------------------------
string[] splitFatsqNames(string fq) {
	string[] empty

	# Empty name? Nothing to do
	if( ! fq )	return(empty)

	names := removeExtFastq( fq ) + "."
	dir := fq.dirName()
	base := removeExtFastq( fq ).baseName() + "."

	# Get all lines matching base name in fq's directory
	return( dir.dirPath(".*/$base..") )
}

